int main()
{
decl
    int temp, pid, a, flag, retcom;
    string input, username, password;
enddecl

begin
    a=1;
    while(a == 1) do
        write("---Enter---");
        read(input);

        flag=0;
        if(input == "Logout" OR input == "Shutdown") then
            flag=1;
        endif;
              
        if(input == "Remusr" OR input == "Getuid") then
            flag=1;
            write("USERNAME");
            read(username);
        endif;

        if(input == "Getuname") then
            flag=1;
            write("USERID");
            read(username);
        endif;
              
        if(input == "Newusr" OR input == "Setpwd") then
            flag=1;
            write("USERNAME");
            read(username);
            write("PASSWORD");
            read(password);
        endif;
              
        if(flag==1) then
            retcom = exposcall(input, username, password);
            if(retcom < 0) then
                write("BAD COMMAND");
            else
                if(input == "Getuid" OR input == "Getuname") then
                    write(retcom);
                endif;
            endif;
        else
            pid = exposcall("Fork");
            if(pid < 0) then
                write("Fork Fail");
                continue;
            endif;
            
            if(pid != 0) then
                temp = exposcall("Wait",pid);
            else
                temp = exposcall("Exec",input);
                if(temp != 0) then
                    write("BAD COMMAND");
                    break;
                endif;
            endif;
        endif;
    endwhile;
          
    return 0;
end
}