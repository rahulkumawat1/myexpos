alias funcNo R1;
alias currPid R2;

//---------------------------------------DISK_STORE----------------------------------

if(funcNo == DISK_STORE) then

    alias page_num R3;
    alias block_num R4;

    multipush(R1,R2,R3,R4);
    R1 = ACQUIRE_DISK;
    R2 = currPid;
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3,R4);

    [DISK_STATUS_TABLE+1] = 0;          //load/store bit
    [DISK_STATUS_TABLE+2] = page_num;   //page_number
    [DISK_STATUS_TABLE+3] = block_num;  //block_number

    store(page_num, block_num);

    [PROCESS_TABLE + 16*currPid + 4] = WAIT_DISK;

    backup;
    call SCHEDULER;
    restore;

    return;
endif;

//---------------------------------------DISK_LOAD-----------------------------------

if(funcNo==DISK_LOAD) then

    multipush(R1,R2,R3,R4);

    R1 = ACQUIRE_DISK;
    R2 = currPid;
    call RESOURCE_MANAGER;

    multipop(R1,R2,R3,R4);

    [DISK_STATUS_TABLE+1] = 0;  //load/store bit
    [DISK_STATUS_TABLE+2] = R3; //page_number
    [DISK_STATUS_TABLE+3] = R4; //block_number

    load(R3,R4);

    [PROCESS_TABLE+16*currPid+4] = WAIT_DISK;

    backup;
    call SCHEDULER;
    restore;

    return;

endif;

//-----------------------------------TERMINAL_WRITE----------------------------------

if(funcNo == TERMINAL_WRITE) then     
    
    //......calling Acquire Terminal
    
    multipush(R1,R2,R3);

    R1 = 8;
    R2 = [SYSTEM_STATUS_TABLE+1];

    call RESOURCE_MANAGER;
    multipop(R1,R2,R3);

    print R3;

    //........calling Release Terminal
    multipush(R1,R2,R3);
    R1 = 9;
    R2 = [SYSTEM_STATUS_TABLE+1];
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3);

    return;
endif;

//-----------------------------------TERMINAL_READ----------------------------------

if(funcNo == TERMINAL_READ) then      

    //........calling Acquire Terminal

    multipush(R1,R2,R3);
    funcNo = 8;
    R2 = currPid;

    call RESOURCE_MANAGER;

    multipop(R1,R2,R3);
    read;       //Requesting to read frm Terminal

    [PROCESS_TABLE + 16*[SYSTEM_STATUS_TABLE+1] + 4] = WAIT_TERMINAL;

    //....invoking Scheduler.........

    backup;

    call SCHEDULER;

    restore;

    alias value R5;

    value = [PTBR + 2*(R3/512)]*512 + (R3%512);

    [value] = [PROCESS_TABLE + 16*[SYSTEM_STATUS_TABLE+1] + 8];      //Input Buffer

    //..Release Terminal for Read is done in console intrupt

    return;
endif;

 
